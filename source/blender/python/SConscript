#!/usr/bin/python
Import ('env')

sources = Split('BPY_interface.c BPY_menus.c') + env.Glob('api2_5x/*.c')

if env['WITH_BF_PYAPI_V24X']==1:
	sources += env.Glob('api2_4x/*.c')

if env['BF_SPLIT_SRC'] == 1:
	numobj = len(sources) 	 
	maxobj = 30 	 

	numlibs = numobj / maxobj 	 
	if (numobj % maxobj): 	 
		numlibs = numlibs + 1 	 
	subsources = [] 	 
	 
	if (env['OURPLATFORM'] == 'win32-mingw'): 	 
		for i in range(numlibs - 1): 	 
			subsources.append(sources[i*maxobj:(i+1)*maxobj]) 	 
		subsources.append(sources[(numlibs-1)*maxobj:])

incs = 'api2_5x ../blenkernel ../blenlib ../blenloader'
incs += ' ../render/extern/include ../radiosity/extern/include'
incs += ' ../makesdna #intern/guardedalloc #intern/bmfont ../imbuf ../include'
incs += ' ' + env['BF_PYTHON_INC']
incs += ' ' + env['BF_OPENGL_INC']

defs = []
if env['OURPLATFORM'] in ('win32-mingw') and env['BF_DEBUG']:
	defs.append('Py_TRACE_REFS')

if env['WITH_BF_QUICKTIME']==1:
	incs += ' ' + env['BF_QUICKTIME_INC']
	defs.append('WITH_QUICKTIME')
	
if env['WITH_BF_OPENEXR'] == 1:
    defs.append('WITH_OPENEXR')

if env['WITH_BF_FFMPEG'] == 1:
    defs.append('WITH_FFMPEG')
	
if env['WITH_BF_PYAPI_V24X']==1:
	defs.append('WITH_PYAPI_V24X')
	incs += ' api2_4x'


if (env['BF_SPLIT_SRC'] == 1) and (env['OURPLATFORM'] == 'win32-mingw'): 	 
	for i in range(numlibs): 	
		env.BlenderLib ( libname='blender_python%d' % (i), sources = subsources[i], includes = Split(incs), defines = defs, libtype=['core','game2'], priority = [60,115] )
else:
	env.BlenderLib ( libname='blender_python', sources = Split(sources), includes = Split(incs), defines = defs, libtype=['core','game2'], priority = [60,115] )
